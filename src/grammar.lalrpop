use std::str::FromStr;
use ast::{Expr, BinOp, Block, Ident, Item, Func};

grammar;

pub Module: Vec<Item> = {
    <Item*>
};

pub Item: Item = {
    <Function> => Item::Func(<>),
    <Type>
};

pub Type: Item = {
    "type" <name:Ident> "{" <methods:Function*> "}" => Item::Type{<>},
};

pub Function: Func = {
    "func" <name:Ident> "(" <params: Comma<Ident>> ")" <body:Block> => Func{<>},
};

pub Statement: Expr = {
    <Ident> "=" <Expr> => Expr::new_assign(<>),
    <Expr>,
};

pub Expr: Expr = {
    <ArithExpr>,
    <Term> "." <Ident> => Expr::new_attrlookup(<>),
    <Expr> "(" <Comma<Expr>> ")" => Expr::new_call(<>),
};

ArithExpr = Tier<ExprOp, Factor>;
Factor = Tier<FactorOp, Term>;

ExprOp: BinOp = {
    "+" => BinOp::Add,
    "-" => BinOp::Sub,
};
FactorOp: BinOp = {
    "*" => BinOp::Mul,
    "/" => BinOp::Div,
};

Block: Block = "{" <SemiColon<Statement>> "}" => Block(<>);

pub Term: Expr = {
    <Num> => Expr::Num(<>),
    <Ident> => Expr::Ident(<>),
    "(" <Expr> ")",
    <Block> => Expr::Block(<>),
};

Num: i32 = <r"[0-9]+"> => i32::from_str(<>).unwrap();
Ident: Ident = <r"[[:alpha:]]\w*"> => Ident(String::from(<>));

Tier<Op, NextTier>: Expr = {
    Tier<Op, NextTier> Op NextTier => Expr::new_op(<>),
    NextTier,
};

Comma<T> = Delimited<T, ",">;
SemiColon<T> = Delimited<T, ";">;

Delimited<T, S>: Vec<T> = {
    <v:(<T> S)*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};
